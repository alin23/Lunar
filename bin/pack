#!/usr/bin/env fish
if not set -q PROJECT_DIR
    set PROJECT_DIR (dirname $DIR)
end

set CODESIGN_CERT "Developer ID Application: Alin Panaitiu (RDDXV84A73)"
set CODESIGN_CERT_INSTALLER "Developer ID Installer: Alin Panaitiu (RDDXV84A73)"
set WORK_DIR "$PROJECT_DIR"
set SCRIPTS_DIR "$PROJECT_DIR/scripts"

command_exists create-dmg || npm i -g create-dmg
# command_exists xcnotary || brew install xcnotary

set BUILD_DIR "$CODESIGNING_FOLDER_PATH"
set TMP_DIR /tmp/Lunar

set APP_TMP "$TMP_DIR/Lunar.app"
set APP_ZIP_TMP "$TMP_DIR/Lunar.zip"

rm -rf $TMP_DIR
mkdir -p $TMP_DIR || exit 1
rsync -avzP "$BUILD_DIR" "$TMP_DIR" || exit 1
find "$APP_TMP" -not -perm 644 -and -not -perm 755 -exec chmod 644 \{\} \;

set filesToSign $APP_TMP/Contents/Frameworks/Sparkle.framework/Versions/Current/Resources/Autoupdate.app/Contents/MacOS/* \
    $APP_TMP/Contents/Frameworks/Sparkle.framework/Versions/Current/Updater.app \
    $APP_TMP/Contents/Frameworks/Sparkle.framework/Versions/Current/Autoupdate \
    $APP_TMP/Contents/Frameworks/Sparkle.framework/Versions/Current/Sparkle \
    $APP_TMP/Contents/Frameworks/Sparkle.framework/Versions/Current/XPCServices/* \
    $APP_TMP/Contents/Frameworks/*.framework \
    $APP_TMP/Contents/Frameworks/*.dylib \
    $APP_TMP/Contents/Resources/ddcctl \
    $APP_TMP/Contents/Resources/edid-decode \
    $APP_TMP

for file in $filesToSign
    codesign -fs "$CODESIGN_CERT" -o runtime --timestamp "$file" || exit 1
end

test -f "$APP_ZIP_TMP" && rm -rf "$APP_ZIP_TMP"
ditto -c -k --sequesterRsrc --keepParent "$APP_TMP" "$APP_ZIP_TMP"

if test "$DISABLE_NOTARIZATION" != 1
    if not codesign -vvv --deep --strict "$APP_TMP"
        echo 'Bad signature'
        exit 1
    end

    set spctlOut (spctl --assess -vvvvv --no-cache --ignore-cache "$APP_TMP" 2>&1)
    if not echo $spctlOut | grep -q 'source=Notarized Developer ID'
        if not echo $spctlOut | grep -q 'source=Unnotarized Developer ID'
            echo 'Bad signature'
            exit 1
        end

        if test "$DISABLE_PACKING" = 1
            # xcnotary notarize -d alin.p32@gmail.com -k altool "$APP_TMP"
            xcrun notarytool submit "$APP_ZIP_TMP" --keychain-profile Alin --wait || exit 1
            xcrun stapler staple "$APP_TMP"
            spctl --assess -vv "$APP_TMP" || exit 1

            test -f "$APP_ZIP_TMP" && rm -rf "$APP_ZIP_TMP"
            ditto -c -k --sequesterRsrc --keepParent "$APP_TMP" "$APP_ZIP_TMP"
        end
    end
end


if test "$DISABLE_PACKING" != 1
    set APP_VERSION (/usr/libexec/PlistBuddy -c "Print CFBundleShortVersionString" "$APP_TMP/Contents/Info.plist" || exit 1)

    ./make-installer dmg

    cd "$PROJECT_DIR" && make appcast
end
